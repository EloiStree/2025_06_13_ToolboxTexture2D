#pragma kernel CSMain

RWTexture2D<float4> m_source;
RWTexture2D<float4> m_result;

int m_width;
int m_height;

[numthreads(8, 8, 1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= m_width || id.y >= m_height)
        return;

    // Structuring element size (3x3)
    int2 offsets[9] =
    {
        int2(-1, -1), int2(0, -1), int2(1, -1),
        int2(-1, 0), int2(0, 0), int2(1, 0),
        int2(-1, 1), int2(0, 1), int2(1, 1)
    };

    float4 maxVal = float4(0, 0, 0, 0);

    // Iterate through neighborhood
    for (int i = 0; i < 9; i++)
    {
        int2 coord = int2(id.x, id.y) + offsets[i];

        // Clamp coordinates to image bounds
        coord.x = clamp(coord.x, 0, m_width - 1);
        coord.y = clamp(coord.y, 0, m_height - 1);

        float4 sample = m_source[coord];
        maxVal = max(maxVal, sample);
    }

    m_result[id.xy] = maxVal;
}
